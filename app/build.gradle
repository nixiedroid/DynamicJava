plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

shadowJar{
    archiveClassifier.set('fat')
    minimize {
        exclude(dependency(':premain'))
    }
}

java {
    group = 'com.nixiedroid'
    version = '1'
    base.archivesName = 'app'
    sourceCompatibility = "11"
    targetCompatibility = "11"
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

def preMainDir = DEFAULT_BUILD_DIR_NAME + "/libs/" + project.name + "-"+ project.version + "-" + shadowJar.archiveClassifier.get() + ".jar"
def preMainClass = "com.nixiedroid.premain.Premain"

application  {
    mainClass = "com.nixiedroid.Main"
    applicationDefaultJvmArgs = ["-javaagent:" + preMainDir]
}

compileJava{
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked" << "--release" << "11"
}


//https://docs.gradle.org/4.9/userguide/working_with_files.html#sec:reproducible_archives
//Make gradle builds deterministic
tasks.withType(AbstractArchiveTask).configureEach {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": version,
                //'Add-Opens': 'java.prefs/java.util.prefs',
                "Main-Class": application.mainClass,
                "Premain-class" : preMainClass,
        )
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":premain")
    implementation project(':core')
    implementation 'org.jetbrains:annotations:24.0.0'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
 //   implementation 'org.apache.bcel:bcel:6.8.2'
}

test {
    useJUnitPlatform()
}
